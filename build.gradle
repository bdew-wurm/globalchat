plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

group "net.bdew.wurm"
version "1.2"

repositories {
    mavenCentral()
    maven { url "http://jcenter.bintray.com" }
    maven { url "http://gotti.no-ip.org/maven/repository" }
    maven { url 'https://jitpack.io' }
}

configurations {
    shadowed
    compile.extendsFrom shadowed
}

dependencies {
    shadowed 'net.dv8tion:JDA:3.8.3_462'
    shadowed 'org.slf4j:slf4j-jdk14:1.7.25'
    compile 'org.gotti.wurmunlimited:server-modlauncher:0.43'
    compile 'com.github.bdew-wurm:bdew_server_mod_tools:v1.0.0'
}

shadowJar {
    configurations = [project.configurations.shadowed]
    archiveFileName = "${project.name}.jar"
    exclude 'natives/*/*'
}

task dist(type: Zip) {
    into("mods", {
        into(project.name, {
            from shadowJar
        })
        from fileTree(dir: 'mods', include: '*')
    })
    archiveFileName = "${project.name}-${project.version}.zip"
}
