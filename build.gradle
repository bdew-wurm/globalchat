plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

group "net.bdew.wurm"
version "1.3"

repositories {
    mavenCentral()
    maven { url "https://gotti.no-ip.org/maven/repository" }
    maven { url 'https://jitpack.io' }
}

configurations {
    shadowed
    compile.extendsFrom shadowed
}

dependencies {
    shadowed 'net.dv8tion:JDA:5.0.0-beta.8'
    shadowed 'org.slf4j:slf4j-jdk14:1.7.36'
    compile 'org.gotti.wurmunlimited:server-modlauncher:0.43'
    compile 'com.github.bdew-wurm:bdew_server_mod_tools:v1.0.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.14.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.14.1'

}

shadowJar {
    configurations = [project.configurations.shadowed]
    archiveFileName = "${project.name}.jar"
    exclude 'natives/*/*'
}

task dist(type: Zip) {
    into("mods", {
        into(project.name, {
            from shadowJar
        })
        from fileTree(dir: 'mods', include: '*')
    })
    archiveFileName = "${project.name}-${project.version}.zip"
}
